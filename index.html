<!doctype html>
<html>
    <head>
        <title>Shaders</title>
        <link rel="stylesheet" href="css/codemirror.css">
        <link rel="stylesheet" href="css/theme/twilight.css">
        <meta charset="utf-8">
        <style>
            body {
                margin: 0px;
                background-color: #000;
                overflow: hidden;
                color: white;
            }
            .editor {
                float: left;
                width: 600px;
                padding: 5px;
            }

            h1 {
                font-family: Monaco;
                font-size: 14px;
                padding-left: 5px;
            }

            .editor button {
                float: right;
            }

            pre {
                border: 1px solid #333;
                padding: 5px;
                margin: 5px;
            }

            .viewer {
                float: right;
                width:
            }

            .example, .hidden {
                display: none;
            }

            select {
                float: right;
            }

        </style>
    </head>
    <body>
        <div class="hidden" id="vertex_prefix">
precision highp float;
precision highp int;

uniform mat4 modelMatrix;
uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;
uniform mat4 viewMatrix;
uniform mat3 normalMatrix;
uniform vec3 cameraPosition;
attribute vec3 position;
attribute vec3 normal;
attribute vec2 uv;
attribute vec2 uv2;
        </div>
        <div class="hidden" id="fragment_prefix">
precision highp float;
precision highp int;

uniform mat4 viewMatrix;
uniform vec3 cameraPosition;
        </div>
        <!-- examples -->
        <div class="example" id="example-1">
            <div class="vert">
varying vec3 v_normal;
void main() {
    v_normal = normalMatrix * normal;
    gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
}
            </div>
            <div class="frag">
varying vec3 v_normal;
void main() {
    gl_FragColor = vec4(v_normal, 1.0);
}
            </div>
        </div>
        <div class="example" id="light-example">
            <div class="vert">
varying vec3 v_normal;
void main() {
    v_normal = normalMatrix * normal;
    gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
}
            </div>
            <div class="frag">
varying vec3 v_normal;
void main() {
    vec3 L = normalize(vec3(1.0, 1.0, 1.0));
    vec3 N = normalize(v_normal);

    vec3 diffuse = dot(N, L) * vec3(1.0);

    gl_FragColor = vec4(diffuse, 1.0);
}
            </div>
        </div>
        <div class="example" id="specular-example">
            <div class="vert">
varying vec3 v_normal;
void main() {
    v_normal = normalMatrix * normal;
    gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
}
            </div>
            <div class="frag">
uniform float specular_coefficient;
uniform float specular_power;

varying vec3 v_normal;

void main() {
    vec3 L = normalize(vec3(1.0, 1.0, 1.0));
    vec3 N = normalize(v_normal);
    vec3 V = vec3(0.0, 0.0, 1.0);
    vec3 H = normalize(normalize(V)+L);

    vec3 specular = vec3(1.0) * specular_coefficient * pow(max(0.0, dot(N, H)), specular_power);
    vec3 diffuse = dot(N, L) * vec3(1.0);

    gl_FragColor = vec4(diffuse + specular, 1.0);
}
            </div>
        </div>

        <div class="example" id="multi-light-example">
            <div class="vert">
varying vec3 v_normal;
varying vec3 P;
void main() {
    v_normal = normalMatrix * normal;
    P = (modelViewMatrix * vec4(position, 1.0)).xyz;
    gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
}
            </div>
            <div class="frag">
uniform float specular_coefficient;
uniform float specular_power;
uniform mat4 modelViewMatrix;

varying vec3 v_normal;
varying vec3 P;

void main() {
    const int NUM_LIGHTS = 3;
    vec3 V = normalize(-P);
    vec3 N = normalize(v_normal);

    vec3 L[NUM_LIGHTS];
    L[0] = normalize((modelViewMatrix * vec4(1.0, 1.0, 1.0, 0.0)).xyz );
    L[1] = normalize((modelViewMatrix * vec4(1.0, 0.0, 0.0, 0.0)).xyz );
    L[2] = normalize((modelViewMatrix * vec4(-1.0, 0.0, 0.0, 0.0)).xyz );

    vec3 Lc[NUM_LIGHTS];
    Lc[0] = vec3(0.8, 0.8 , 0.8);
    Lc[1] = 0.2 * vec3(0.6, 0.6, 0.7);
    Lc[2] = 0.2 * vec3(0.7, 0.6, 0.5);

    vec3 specular = vec3(0.0);
    vec3 diffuse = vec3(0.0);
    
    for(int i=0; i &lt; NUM_LIGHTS; i++) {

        float NdL = clamp(dot(N, L[i]), 0.0, 1.0);
        
        diffuse = diffuse + Lc[i]*NdL;

        vec3 H = normalize(V+L[i]);
        specular = specular + specular_coefficient * Lc[i] * pow(max(0.0, dot(N, H)), specular_power);
    }


    gl_FragColor = vec4(diffuse + specular, 1.0);
}
            </div>
        </div>

        <div class="example" id="alternate-specular-example">
            <div class="vert">
varying vec3 v_normal;
varying vec3 P;
void main() {
    v_normal = normalMatrix * normal;
    P = (modelViewMatrix * vec4(position, 1.0)).xyz;
    gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
}
            </div>
            <div class="frag">
uniform mat4 modelViewMatrix;
uniform float specular_coefficient;
uniform float specular_power;

varying vec3 v_normal;
varying vec3 P;

void main() {
    const int NUM_LIGHTS = 3;
    vec3 V = normalize(-P);
    vec3 N = normalize(v_normal);

    vec3 L[NUM_LIGHTS];
    L[0] = normalize((modelViewMatrix * vec4(1.0, 1.0, 1.0, 0.0)).xyz );
    L[1] = normalize((modelViewMatrix * vec4(1.0, 0.0, 0.0, 0.0)).xyz );
    L[2] = normalize((modelViewMatrix * vec4(-1.0, 0.0, 0.0, 0.0)).xyz );

    vec3 Lc[NUM_LIGHTS];
    Lc[0] = vec3(0.8, 0.8 , 0.8);
    Lc[1] = 0.2 * vec3(0.6, 0.6, 0.7);
    Lc[2] = 0.2 * vec3(0.7, 0.6, 0.5);

    vec3 specular = vec3(0.0);
    vec3 diffuse = vec3(0.0);
    
    for(int i=0; i &lt; NUM_LIGHTS; i++) {

        float NdL = dot(N, L[i]);
        
        diffuse = diffuse + Lc[i]*clamp(NdL, 0.0, 1.0);

        if (NdL > 0.0) {
            vec3 H = normalize(V+L[i]);
            
            float w = pow(1.0 - max(0.0, dot(H, V)), 
                          5.0);
            
            specular = specular + specular_coefficient * Lc[i] 
                * mix(Lc[i], vec3(1.0), w)
                * pow(max(0.0, dot(reflect(-L[i], N), N)),
                      specular_power);
        }
    }


    gl_FragColor = vec4(diffuse + specular, 1.0);
}
            </div>
        </div>

        <div class="example" id="fresnel-example">
            <div class="vert">
varying vec3 v_normal;
varying vec3 P;
void main() {
    v_normal = normalMatrix * normal;
    P = (modelViewMatrix * vec4(position, 1.0)).xyz;
    gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
}
            </div>
            <div class="frag">

uniform mat4 modelViewMatrix;
uniform float specular_coefficient;
uniform float specular_power;

varying vec3 v_normal;
varying vec3 P;

void main() {
    const int NUM_LIGHTS = 3;
    vec3 V = normalize(-P);
    vec3 N = normalize(v_normal);

    vec3 L[NUM_LIGHTS];
    L[0] = normalize((modelViewMatrix * vec4(1.0, 1.0, 1.0, 0.0)).xyz );
    L[1] = normalize((modelViewMatrix * vec4(1.0, 0.0, 0.0, 0.0)).xyz );
    L[2] = normalize((modelViewMatrix * vec4(-1.0, 0.0, 0.0, 0.0)).xyz );

    vec3 Lc[NUM_LIGHTS];
    Lc[0] = vec3(0.8, 0.8 , 0.8);
    Lc[1] = 0.2 * vec3(0.6, 0.6, 0.7);
    Lc[2] = 0.2 * vec3(0.7, 0.6, 0.5);

    vec3 specular = vec3(0.0);
    vec3 diffuse = vec3(0.0);
    
    for(int i=0; i &lt; NUM_LIGHTS; i++) {

        float NdL = dot(N, L[i]);
        
        diffuse = diffuse + Lc[i]*clamp(NdL, 0.0, 1.0);

        if (NdL > 0.0) {
            vec3 H = normalize(V+L[i]);
            
            float w = pow(1.0 - max(0.0, dot(H, V)), 
                          5.0);
            
            specular = specular + specular_coefficient * Lc[i] 
                * mix(Lc[i], vec3(1.0), w)
                * pow(max(0.0, dot(reflect(-L[i], N), N)),
                      specular_power);
        }
    }

    float facing = 1.0 - dot(V, N);
    vec3 rim_color = vec3(0.5);
    vec3 rim = rim_color * smoothstep(0.5, 1.0, facing) * (specular+diffuse);

    gl_FragColor = vec4(specular + diffuse + rim, 1.0);
}
          
            </div>
        </div>

        <div class="example" id="cook-torrence-example">
            <div class="vert">
varying vec3 v_normal;
varying vec3 P;
void main() {
    v_normal = normalMatrix * normal;
    P = (modelViewMatrix * vec4(position, 1.0)).xyz; 
    gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
}
            </div>
            <div class="frag">

uniform mat4 modelViewMatrix;
uniform float specular_coefficient;
uniform float specular_power;

varying vec3 v_normal;
varying vec3 P;

void main() {
    const int NUM_LIGHTS = 3;
    vec3 V = normalize(-P);
    vec3 N = normalize(v_normal);

    vec3 L[NUM_LIGHTS];
    L[0] = normalize((modelViewMatrix * vec4(1.0, 1.0, 1.0, 0.0)).xyz );
    L[1] = normalize((modelViewMatrix * vec4(1.0, 0.0, 0.0, 0.0)).xyz );
    L[2] = normalize((modelViewMatrix * vec4(-1.0, 0.0, 0.0, 0.0)).xyz );

    vec3 Lc[NUM_LIGHTS];
    Lc[0] = vec3(0.8, 0.8 , 0.8);
    Lc[1] = 0.2 * vec3(0.6, 0.6, 0.7);
    Lc[2] = 0.2 * vec3(0.7, 0.6, 0.5);

    vec3 specular = vec3(0.0);
    vec3 diffuse = vec3(0.0);
    
    for(int i=0; i &lt; NUM_LIGHTS; i++) {

        vec3 H = normalize(V+L[i]);
        float NdL = clamp(dot(N, L[i]), 0.0, 1.0);
        float NdH = clamp(dot(N, H), 0.0, 1.0);
        float FG = 0.25 / (pow(NdH, 3.0) + 1.0/32.0);
        float D = 0.5 * (specular_power + 1.0) * pow(NdH, specular_power);
        float DFG = D * FG;
        
        specular = specular + Lc[i] * NdL * DFG * specular_coefficient;
        diffuse = diffuse + Lc[i] * NdL;
    }


    gl_FragColor = vec4(diffuse + specular, 1.0);
}
          
            </div>
        </div>

        <div class="example" id="texture-example">
            <div class="vert">
varying vec2 v_uv;
varying vec3 v_normal;
void main() {
    v_uv = uv;
    v_normal = normalMatrix * normal;
    gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
}
            </div>
            <div class="frag">
uniform sampler2D texture;
uniform mat4 modelViewMatrix;

varying vec2 v_uv;
varying vec3 v_normal;

void main() {
    vec3 diffuse_color = texture2D(texture, v_uv).rgb;
    vec3 Lc = vec3(0.8);
    vec3 L = (modelViewMatrix * vec4(1.0, 1.0, 1.0, 0.0)).xyz;
    vec3 ambient = vec3(0.2);
    
    float NdL = clamp(dot(v_normal, L), 0.0, 1.0);
    vec3 diffuse_light = Lc * NdL + ambient;
    
    gl_FragColor = vec4(diffuse_light*diffuse_color, 1.0);
}
            </div>
        </div>
        <div class="example" id="normal-map-example">
            <div class="vert">
attribute vec4 tangent;

varying vec2 v_uv;
varying mat3 TBN;

void main() {
    vec3 N = normalize(normalMatrix * normal);
    vec3 T = normalize(normalMatrix * tangent.xyz);
    vec3 B = normalize(normalMatrix * cross(normal, tangent.xyz) * tangent.w);
    TBN = mat3(
       T.x, T.y, T.z,
       B.x, B.y, B.z,
       N.x, N.y, N.z
    );

    v_uv = uv;

    gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
}
            </div>
            <div class="frag">
uniform sampler2D texture;
uniform sampler2D day_diffuse_map;
uniform sampler2D normal_map;

uniform mat4 modelViewMatrix;

varying vec2 v_uv;
varying mat3 TBN;

vec3 getNormal()
{
    vec3 Nm = texture2D(normal_map, v_uv).xyz;
    Nm *= 2.0;
    Nm -= vec3(1.0);
    return normalize(TBN * Nm);
}

void main() {
    vec3 diffuse_color = texture2D(day_diffuse_map, v_uv).rgb;

    vec3 Lc = vec3(0.8);
    vec3 L = (modelViewMatrix * vec4(1.0, 1.0, 1.0, 0.0)).xyz;
    vec3 ambient = vec3(0.2);
    
    vec3 N = getNormal();
    float NdL = clamp(dot(N, L), 0.0, 1.0);

    vec3 diffuse_light = Lc * NdL + ambient;
    
    gl_FragColor = vec4(diffuse_light*diffuse_color, 1.0);
}
            </div>
        </div>

        <div class="example" id="earth-example">
            <div class="vert">
attribute vec4 tangent;

varying vec2 v_uv;
varying mat3 TBN;
varying vec3 P;
void main() {
    vec3 N = normalize(normalMatrix * normal);
    vec3 T = normalize(normalMatrix * tangent.xyz);
    vec3 B = normalize(normalMatrix * cross(normal, tangent.xyz) * tangent.w);
    TBN = mat3(
       T.x, T.y, T.z,
       B.x, B.y, B.z,
       N.x, N.y, N.z
    );
    v_uv = uv;
    P = (modelViewMatrix * vec4(position, 1.0)).xyz;
    gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
}
            </div>
            <div class="frag">



uniform mat4 modelViewMatrix;
uniform float time;

uniform sampler2D cloud_map;
uniform sampler2D specular_map;
uniform sampler2D day_diffuse_map;
uniform sampler2D night_diffuse_map;
uniform sampler2D normal_map;
uniform sampler2D glow_map;


varying vec2 v_uv;
varying mat3 TBN;
varying vec3 P;

vec3 getNormal()
{
    vec3 Nm = texture2D(normal_map, v_uv).xyz;
    Nm *= 2.0;
    Nm -= vec3(1.0);
    return normalize(TBN * Nm);
}

void main() {
    vec3 V = normalize(-P);
    vec3 N = getNormal();
    vec3 sphereN = TBN[2];

    // light
    vec3 L = normalize((modelViewMatrix * vec4(1.0, 1.0, 1.0, 0.0)).xyz );
    vec3 Lc = vec3(0.9, 0.9 , 0.8);

    // diffuse
    float NdL = max(0.0, dot(N, L));
    float sphereNdL = max(0.0, dot(sphereN, L));
    vec3 diffuse =  Lc*NdL;

    // specular
    const float specular_coefficient = 0.5;
    const float specular_power = 25.0;
    const float earth_spec = 0.6;
    const float sea_spec = 0.8;
    vec3 H = normalize(V+L);
    float spec_amount = mix(earth_spec, sea_spec, texture2D(specular_map, v_uv).r);
    float NdH = clamp(dot(N, H), 0.0, 1.0);
    float FG = 0.25 / (pow(NdH, 3.0) + 1.0/32.0);
    float D = spec_amount * 0.5 * (specular_power + 1.0) * pow(NdH, specular_power * spec_amount);
    float DFG = D * FG;
    vec3 specular = spec_amount * specular_coefficient * Lc * NdL * DFG;
    
    // fresnel
    float facing = 1.0 - max(0.0, dot(V, sphereN));
    
    // day/night mixing
    vec3 day_color = texture2D(day_diffuse_map, v_uv).rgb;
    vec3 night_color = texture2D(night_diffuse_map, v_uv).rgb;
    vec3 diffuse_color = mix(night_color, diffuse*day_color, sphereNdL);

    // basic light model
    vec3 globe = diffuse_color + specular;
    
    // glow
    vec3 glow = 3.0 * texture2D(glow_map, v_uv).rgb;
    globe += glow * clamp(1.0-2.0*sphereNdL, 0.0, 1.0);
    
    // cloud coverage 
    const vec2 cloud_velocity = vec2(0.012, 0.0);
    const float cloud_min = 0.15;
    const float cloud_max = 1.0;
    vec4 cloud = texture2D(cloud_map, v_uv + time*cloud_velocity);
    
    globe = mix(globe, cloud.rgb, cloud_min * cloud.a);
    globe = mix(globe, cloud_max * cloud.rgb, facing * sphereNdL * cloud.a);
    
    // lets pretend the globe is a bit smaller
    globe *= 1.0 - smoothstep(0.75, 0.83, facing);
    
    // make the atmosphere 
    const float min_atmo_amount = 0.03;
    const vec3 atmo_color = vec3(0.8, 0.9, 1.0);
    vec3 atmosphere = atmo_color * (min_atmo_amount + sphereNdL);
    vec3 fresnatmosphere = smoothstep(0.3, 1.0, facing) * atmosphere;
    
    // make the atmosphere hazy and soft
    float fade = smoothstep(1.0, 0.7, facing);
    vec3 atmo = fade * fresnatmosphere;

    // final color
    gl_FragColor = vec4(globe + atmo, 1.0); 
}
            </div>
        </div>
        <div class="example" id="reflection-example">
            <div class="vert">
attribute vec4 tangent;

varying vec3 v_normal;
varying vec2 v_uv;
varying mat3 TBN;

void main() {
    vec3 N = normalize(normalMatrix * normal);
    vec3 T = normalize(normalMatrix * tangent.xyz);
    vec3 B = normalize(normalMatrix * cross(normal, tangent.xyz) * tangent.w);
    TBN = mat3(
       T.x, T.y, T.z,
       B.x, B.y, B.z,
       N.x, N.y, N.z
    );

    v_uv = uv;
    v_normal = normalMatrix * normal;
    gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
}
            </div>
            <div class="frag">
uniform sampler2D reflection_map;
uniform sampler2D normal_map;

varying vec3 v_normal;
varying vec2 v_uv;
varying mat3 TBN;

vec3 getNormal()
{
    vec3 Nm = texture2D(normal_map, v_uv).xyz;
    Nm *= 2.0;
    Nm -= vec3(1.0);
    return normalize(TBN * Nm);
}

void main() {
    // vec3 N = normalize(v_normal);
    vec3 N =  getNormal();
    vec2 uv = N.xy / 2.0 + vec2(0.5);
    vec3 color = texture2D(reflection_map, uv).rgb;
    gl_FragColor = vec4(color, 1.0);
}
            </div>
        </div>

        <!-- examples -->

        <div class="editor">
        <select>
            <option value="example-1">Normals</option>
            <option value="light-example">Lighting</option>
            <option value="specular-example">Specular</option>
            <option value="multi-light-example">Multiple Lights</option>
            <option value="alternate-specular-example">Alternate Specular</option>
            <option value="fresnel-example">Fresnel</option>
            <option value="texture-example">Texture</option>
            <option value="normal-map-example">Normal Map</option>
            <option value="reflection-example" selected>Reflection</option>
            <option value="cook-torrence-example">Cook-Torrence</option>
            <option value="earth-example">Earth</option>
        </select>
        <h1>Vertex Shader</h1>
        <pre id="vertexShaderEditor"></pre>
        <h1>Fragment Shader</h1>
        <pre id="fragmentShaderEditor"></pre>
        <button name="apply" id="apply">Apply</button>
        </div>

        <div class="viewer">
        </div>

        <script src="http://code.jquery.com/jquery-1.11.1.min.js"></script>
        <script src="http://threejs.org/build/three.min.js"></script>
        <script src="js/codemirror.js"></script>
        <script src="js/mode/clike/clike.js"></script>
        <script src="js/dat.gui.min.js"></script>

        <script>
            var fragEditor, vertEditor;
            var camera, scene, renderer;
            var dirLight;
            var mesh;
            var material;
            var shader_parameters = {
                "time": {
                    type: 'f',
                    value: 0.0
                },
                "texture": {
                    type: 't',
                    value: THREE.ImageUtils.loadTexture("textures/tracking.png")
                },
                "cloud_map": {
                    type: 't',
                    value: THREE.ImageUtils.loadTexture("textures/clouds.png")
                },
                "specular_map": {
                    type: 't',
                    value: THREE.ImageUtils.loadTexture("textures/earth-SPEC.png")
                },
                "day_diffuse_map": {
                    type: 't',
                    value: THREE.ImageUtils.loadTexture("textures/earth-day-DIFF.png")
                },
                "night_diffuse_map": {
                    type: 't',
                    value: THREE.ImageUtils.loadTexture("textures/earth-night-DIFF.png")
                },
                "glow_map": {
                    type: 't',
                    value: THREE.ImageUtils.loadTexture("textures/earth-night-GLOW.png")
                },
                "normal_map": {
                    type: 't',
                    value: THREE.ImageUtils.loadTexture("textures/earth-NM.png")
                },
                "reflection_map": {
                    type: 't',
                    value: THREE.ImageUtils.loadTexture("textures/reflection.jpg")
                },
                "specular_coefficient": { 
                    type: 'f', 
                    value: 1.0, 
                    min: 0.0001, 
                    max: 8.0 
                },
                "specular_power": { 
                    type: 'f', 
                    value: 30.0, 
                    min: 0.0001, 
                    max: 256.0
                }
            };

            function setupGui() {
                var gui_params = {};
                $.each(shader_parameters, function(name, param) {
                    if (param.type != "t")
                        gui_params[name] = param.value;
                });

                var gui = new dat.gui.GUI();

                $.each(gui_params, function(name, value) {
                    gui.add(gui_params, name, 
                        shader_parameters[name].min, 
                        shader_parameters[name].max).onChange(function(v) {
                            shader_parameters[name].value = v;
                            shader_parameters[name].needsUpdate = true;
                        }
                    );
                });
            }

            function main() {
                setupGui();
                init();
                animate();
            }

            function compileShader(editor, type) {
                var gl = renderer.context;
                var shader = gl.createShader(type);
                var src = editor.getDoc().getValue();
                console.log(src);
                console.log(type);

                var prefix = "";
                if (type == gl.VERTEX_SHADER)
                    prefix = $("#vertex_prefix").text();
                else
                    prefix = $("#fragment_prefix").text();

                gl.shaderSource(shader, prefix + src);
                gl.compileShader(shader);

                if (!gl.getShaderParameter(shader, gl.COMPILE_STATUS)) {
                    console.log(gl.getShaderParameter(shader, gl.COMPILE_STATUS));
                    return false;
                }

                return true;
            }

            function applyShaderIfCompiles() {
                console.log("testing");
                var gl = renderer.context;
                if (compileShader(vertEditor, gl.VERTEX_SHADER) && compileShader(fragEditor, gl.FRAGMENT_SHADER))
                    createMesh();
            }

            function createMesh() {
                var rotation = 0;
                if (mesh) {
                    rotation = mesh.rotation.y;
                    scene.remove(mesh);
                }

                //var texture = THREE.ImageUtils.loadTexture( 'textures/crate.gif' );
                //texture.anisotropy = renderer.getMaxAnisotropy();

                material = new THREE.ShaderMaterial({ 
                    uniforms: shader_parameters,
                    vertexShader: vertEditor.getDoc().getValue(),
                    fragmentShader: fragEditor.getDoc().getValue()
                });

                var geometry = new THREE.SphereGeometry( 100, 64, 64 );
                geometry.computeTangents();
                mesh = new THREE.Mesh(geometry, material);
                scene.add(mesh);
                mesh.rotation.y = rotation;
            }

            function init() {
                var view_width = window.innerWidth-$(".editor").outerWidth();
                var view_height = window.innerHeight;

                shader_parameters.cloud_map.value.wrapS = shader_parameters.cloud_map.value.wrapT = THREE.RepeatWrapping;

                renderer = new THREE.WebGLRenderer();
                renderer.setSize( view_width, view_height );
                $(".viewer").append( renderer.domElement );

                camera = new THREE.PerspectiveCamera( 70, view_width / view_height, 1, 1000 );
                camera.position.z = 400;

                scene = new THREE.Scene();

                createMesh();

                window.addEventListener( 'resize', onWindowResize, false );

            }

            function onWindowResize() {
                var view_width = window.innerWidth-$(".editor").outerWidth();
                var view_height = window.innerHeight;

                camera.aspect = (view_width / view_height);
                camera.updateProjectionMatrix();

                renderer.setSize(view_width, view_height);
            }

            function animate() {

                requestAnimationFrame( animate );

                mesh.rotation.y += 0.005;

                shader_parameters.time.value += 0.01;
                shader_parameters.time.needsUpdate = true;

                renderer.render( scene, camera );

            }

            function getActiveExample() {
                return $("select option:selected").val();
            }

            function applySelectedShaders() {
                var example = getActiveExample();
                fragEditor.getDoc().setValue($("#"+example+" .frag").text());
                vertEditor.getDoc().setValue($("#"+example+" .vert").text());
                createMesh();
            }

            $("select").change(applySelectedShaders);

            $("#apply").click(createMesh);
            $(function() {
                var example = getActiveExample();
                var initial_fragment_shader = $("#"+example+" .frag").text();
                var initial_vertex_shader = $("#"+example+" .vert").text();

                vertEditor = CodeMirror(document.getElementById("vertexShaderEditor"),
                    {
                        value: initial_vertex_shader,
                        mode: "x-shader/x-vertex",
                        indentUnit: 4,
                        theme: "twilight"
                    }
                );

                fragEditor = CodeMirror(document.getElementById("fragmentShaderEditor"),
                    {
                        value: initial_fragment_shader,
                        mode: "x-shader/x-fragment",
                        indentUnit: 4,
                        onChange: applyShaderIfCompiles,
                        theme: "twilight"
                    }
                );

                vertEditor.on("change", applyShaderIfCompiles);
                fragEditor.on("change", applyShaderIfCompiles);
            });

            $(main);
        </script>
    </body>
</html>
